import groovy.json.JsonSlurper

plugins {
	id "fabric-loom" version "1.4-SNAPSHOT"
	id "maven-publish"
	// todo: use https://github.com/modmuss50/mod-publish-plugin when more fleshed out
	id "com.modrinth.minotaur" version "2.+"
	id "com.matthewprenger.cursegradle" version "1.4.0"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = rootProject.archives_base_name
group = rootProject.group
version = rootProject.version
// tokens revert to empty strings when no secrets.json exists
def tokens = new JsonSlurper().parseText( file("secrets.json").exists() ? file("secrets.json").text : "{\"modrinth\": \"\", \"curseforge\": \"\"}" )
def changelogText = "Changelog could not be found... ðŸ˜¬"
def noPublish = project.hasProperty("noPublish") ? Boolean.parseBoolean(project.getProperty("noPublish")) : false

repositories {
  	mavenCentral()

	maven { url "https://maven.quiltmc.org/repository/release/" } // Quilt
	//maven { url "https://jitpack.io" } // MixinExtras
	maven { url "https://maven.terraformersmc.com/releases/" } // Mod Menu
	maven { url "https://maven.isxander.dev/releases" } // YACL
	maven { url "https://oss.sonatype.org/content/repositories/snapshots/" } // temporary fix for a fix for YACL (twelvemonkeys imageio)
}

dependencies {
	minecraft "com.mojang:minecraft:$project.minecraft"
	mappings "net.fabricmc:yarn:$project.minecraft$project.yarn:v2"
	modImplementation "net.fabricmc:fabric-loader:$project.loader"
	modImplementation "net.fabricmc.fabric-api:fabric-api:$project.api"

	// Jar-In-Jar, add MixinExtras, and apply annotation processor
	include(implementation(annotationProcessor("io.github.llamalad7:mixinextras-fabric:$project.mixin_extras")))

	modImplementation "com.terraformersmc:modmenu:$project.modmenu"
	modImplementation "dev.isxander.yacl:yet-another-config-lib-fabric:$project.yacl"


	// integration files, toggleable in /integrations/integrations.properties
	// put jar files in /integrations/ and see the .properties file for more info
	File integrationsFile = file("./integrations/integrations.properties")
	if( integrationsFile.exists() ) {
		println "Loading dev testing integrations..."

		def integrations = new Properties(); integrationsFile.withInputStream { integrations.load(it) }
		def loaded = 0
		def all = 0

		for(integration in integrations) {
			def name = integration.toString().split("=")[0]
			def data = integration.toString().split("=")[1].split(",")
			def src = data[0].endsWith(".jar") ? "./integrations/" + data[0] : data[0]
			def enabled = data[1].equalsIgnoreCase("true")
			def func = data.size() > 2 ? data[2] : "modImplementation"


			if(enabled) {
				if( src.endsWith(".jar") && !file(src).exists() ) {

					System.err.println("Integration jar '" + src + "' doesn't exist")
					enabled = false
				} else {
					"${func}" (src.endsWith(".jar") ? files(src) : src)
					++loaded
				}
			}

			println( String.format("\t-> %s integration '%s' with '%s' %s",
				enabled ? "Loaded" : "Skipped",
				name,
				src,
				(func != "modImplementation" ? "using method '$func'" : "")
			))

			++all
		}
		println "Finished loading $loaded/$all dev testing integrations\n"
	}
}

processResources {
	inputs.property "version", version

	filesMatching("fabric.mod.json") {
		expand "version": version

		/* filter {
			it.replace("example_key", rootProject.exampleVar)
		} */
	}

	File changelog = file("changelog.md")
	if( changelog.exists() ) {
		// replaces github issue numbers with links and fills out some variables
		file("changelog.md").text = changelog.text
			.replaceAll("##(\\d+)", "[#\$1](https://www.github.com/mrbuilder1961/ChatPatches/issues/\$1)")
			.replaceAll("\\\$\\{version}", version.toString())
			.replaceAll("\\\$\\{targets}", targets.replaceAll(",", ", "))
			.replaceAll("\\\$\\{loaders}", loaders.split(",").collect { it.capitalize() }.join(", "))


		// hackily gets the first changelog entry
		String newEntryTitle = "## Chat Patches `" + version + "`"
		int prevEntryIndex = file("changelog.md").text.replaceFirst(newEntryTitle, "").indexOf("## Chat Patches `") + newEntryTitle.length() - 2

		changelogText = file("changelog.md").text.substring(0, prevEntryIndex).replaceFirst("# Changelog\\s+", "")

		// considers the changelog "malformed" if it doesn't end with word character(s), whitespace, or newline(s)
		if( !changelogText.matches("(?s).*(\\s+|(\r?\n)+|\\w+)\$") ) {
			noPublish = true
			println "/!\\ Warning: /!\\ Changelog seemed malformed, this is probably caused by an invalid version (v$version). Cancelled publishing, just in case."
		}

		if(noPublish)
			println "Using changelog text (__=start and end):\n__${changelogText}__"
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archives_base_name}"}
	}
}

build {
	dependsOn tasks.processResources
}


publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {}
}


publish {
	dependsOn tasks.processResources, tasks.curseforge, tasks.modrinth
}

curseforge { // https://github.com/matthewprenger/CurseGradle/wiki/
	apiKey = tokens.curseforge
	project {
		id = "560042"
		releaseType = "release"
		changelogType = "markdown"
		changelog = changelogText

		// adds Java versions, modloaders, and mc versions
		addGameVersion "Java 17"
		addGameVersion "Java 18"
		for(String target : rootProject.targets.split(","))
			addGameVersion target
		for(String loader : rootProject.loaders.split(","))
			addGameVersion loader.capitalize()

		mainArtifact remapJar

		relations {
			requiredDependency "fabric-api"
			requiredDependency "yacl"

			optionalDependency "modmenu"

			tool "no-chat-reports"

			incompatible "more-chat-history"
		}

		options {
			forgeGradleIntegration = false
			debug = noPublish
		}
	}
}

modrinth { // https://github.com/modrinth/minotaur#groovy
	token = tokens.modrinth
	projectId = "MOqt4Z5n"
	versionNumber = version // Will fail if Modrinth has this version already
	versionType = "release"
	uploadFile = remapJar
	gameVersions = Arrays.asList( rootProject.targets.split(",") ) // Must be an array
	loaders = Arrays.asList( rootProject.loaders.split(",") )
	changelog = changelogText

	debugMode = noPublish

	dependencies {
		required.project "fabric-api"
		required.project "yacl"

		optional.project "modmenu"
		optional.project "no-chat-reports"

		incompatible.project "morechathistory"
	}
}
